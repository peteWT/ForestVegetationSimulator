      SUBROUTINE VOLS
      IMPLICIT NONE
C----------
C IE $Id$
C----------
C
C  THIS SUBROUTINE CALCULATES TREE VOLUMES TO THREE MERCHANTABILITY
C  STANDARDS, AND COMPUTES DISTRIBUTION AND COMPOSITION VECTORS FOR
C  TOTAL CUBIC VOLUME AND ACCRETION.  TOTAL CUBIC FOOT VOLUME AND
C  MERCHANTABLE CUBIC FOOT VOLUME ARE COMPUTED IN **CFVOL**,
C  **NATCRS**, OR **OCFVOL**.  BOARD FOOT VOLUME IS COMPUTED IN
C  **BFVOL**, **NATCRS**, OR **OBFVOL**.  ADJUSTMENTS ARE MADE FOR
C  TOP DAMAGE AS APPROPRIATE.  ALL VOLUMES ARE THEN ADJUSTED FOR
C  DEFECT.
C
C  NATCRS, OCFVOL, AND OBFVOL ARE ENTRY POINTS IN SUBROUTINE
C  **VARVOL**, WHICH IS VARIANT SPECIFIC.
C----------
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'COEFFS.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'OUTCOM.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'VOLSTD.F77'
C
C
      INCLUDE 'GGCOM.F77'
C
C
      INCLUDE 'WDBKWTDATA.INC' 
COMMONS
C
C----------
C  DIMENSIONS FOR INTERNAL VARIABLES.
C
C  SPCCC -- TOTAL CUBIC VOLUME BY SPECIES AND TREE CLASS.
C  SPCAC -- TOTAL CUBIC VOLUME ACCRETION BY SPECIES AND TREE CLASS.
C  SPCMC -- MERCH CUBIC VOLUME BY SPECIES AND TREE CLASS.
C  SPCBV -- MERCH BOARD VOLUME BY SPECIES AND TREE CLASS.
C
C  THESE VALUES DO NOT INCLUDE CYCLE 0 DEAD TREES.
C  IPASS=1 FOR LIVE TREES;  IPASS=2 FOR CYCLE 0 DEAD TREES.
C
C  BTKFLG - LOGICAL VARIABLES TO INDICATE WHETHER THE VOLUME ESTIMATE
C  CTKFLG   NEEDS TO BE ADJUSTED FOR TOPKILL OR NOT.
C           BTKFLG FOR BOARD FOOT VOLUME
C           CTKFLG FOR CUBIC FOOT VOLUME
C           .TRUE.  = ADJUST ESTIMATE FOR TOPKILL
C           .FALSE. = NO ADJUSTMENT NEEDED (ALREADY ACCOUNTED FOR).
C----------
      INTEGER DLIEQN,DLLMOD
      LOGICAL DEBUG
      REAL SPCCC(MAXSP,3),SPCAC(MAXSP,3),SPCBV(MAXSP,3),SPCMC(MAXSP,3)
      REAL SPCSC(MAXSP,3)
      REAL DBHCLS(9),P,D,H,BARK,BRATIO,CARBFACTOR
      REAL D2H,VM,VN,VMAX,BFMAX,BBFV,ALGSLP,VOLCOR,TEMVOL,PULPV
      REAL TCF,MCF,SCF,BIOMAS(15)
      INTEGER I,J,K,IPASS,ILOW,IHI,ISPC,IT,IM,ICDF,IBDF,IFIASP
      LOGICAL TKILL,LCONE,BTKFLG,CTKFLG
      CHARACTER LIVEDEAD
C----------
C  INITIALIZE DBH CLASSES.
C----------
      DATA DBHCLS/0.0,5.0,10.0,15.0,20.0,25.0,30.0,35.0,40.0/
C-----------
C  CHECK FOR DEBUG.
C-----------
      CALL DBCHK (DEBUG,'VOLS',4,ICYC)
      DO J=1,2
      DO I=1,MAXTRE
      HT2TD(I,J)=0.
      ENDDO
      ENDDO
      IF (ITRN.GT.0) THEN 
        SPCCC=0.0
        SPCAC=0.0
        SPCMC=0.0
        SPCSC=0.0
        SPCBV=0.0
      ENDIF
      IPASS=1
      ILOW=1
      IHI=ITRN
C----------
C  CALL VOLKEY TO PROCESS KEYWORDS USED TO CHANGE VOLUME STANDARDS AND
C  EQUATIONS.
C----------
      CALL VOLKEY (DEBUG)
      DO J=1,MAXSP
      IF(BFMIND(J).LT.2.0) BFMIND(J)=2.0
      IF(BFTOPD(J).LT.2.0) BFTOPD(J)=2.0
      ENDDO
C
      IF (ITRN.LE.0) GOTO 205
C----------
C  ENTER TREE BY TREE LOOP TO CALCULATE VOLUMES AND COMPILE
C  SUMMARY ARRAYS.
C
C  I -- SUBSCRIPT TO TREE RECORD.
C  P -- NUMBER OF TREES PER ACRE REPRESENTED BY TREE I.
C  D -- DIAMETER OF TREE I.
C  H -- HEIGHT OF TREE I.
C  ISPC -- SPECIES OF TREE I.
C  WK5 -- ARRAY CONTAINING TOTAL ANNUAL CUBIC VOLUME ACCRETION
C    PER ACRE FOR EACH TREE.  THIS ARRAY IS NOT LOADED
C    IF LSTART IS TRUE.
C  VN -- USED TO TEMPORARILY STORE TOTAL CUBIC VOLUME FOR TREE I.
C----------
   10 CONTINUE
      DO 200 I=ILOW,IHI
      WK5(I)=0.0
      P=PROB(I)
      IF(P.LE.0.0) GO TO 200
      ISPC=ISP(I)
      D=DBH(I)
      H=HT(I)

      IF(IMC(I) .GE. 6) THEN 
        LIVEDEAD = 'D'
      ELSE
        LIVEDEAD = 'L'
      END IF

C----------
C  INITIALIZE TOP KILL FLAG FOR NEXT TREE; IF TOPKILLED, ASSIGN H TO
C  NORMHT.
C----------
      TKILL = H.GE.4.5 .AND. ITRUNC(I).GT.0
      IF(TKILL) H=NORMHT(I)/100.0
C----------
C  IF NOT INITIAL SUMMARY, ADD DG TO DBH; ASSIGN D2H.
C----------
      BARK=BRATIO(ISPC,D,H)
      IF(.NOT.LSTART) D=D+DG(I)/BARK
      D2H=D*D*H
C**************************************************
C              CUBIC VOLUME SECTION               *
C**************************************************
C----------
C  INITIALIZE VOLUME ESTIMATES.
C----------
      VN=0.0
      VM=0.0

      TCF=0.0
      MCF=0.0
      SCF=0.0

      VMAX=0.0
      BFMAX=0.0
      BBFV=0.0

      BIOMAS = 0.
      IF(DEBUG)WRITE(JOSTND,*)' CUBIC SECTION, I,ISPC,METHC= ',
     &I,ISPC,METHC(ISPC)
C----------
C  CALCULATE TOTAL CUBIC FOOT VOLUME. CORRECT FOR TOP KILL IF NEEDED.
C----------
      IT=I
      IF((METHC(ISPC).EQ.6 .OR. METHC(ISPC).EQ.10) .OR. 
     1   (METHC(ISPC).EQ.5 .AND. 
     1   (VARACD.EQ.'CS' .OR. VARACD.EQ.'LS' .OR. VARACD.EQ.'NE')))THEN
        CALL NATCRS (TCF,MCF,SCF,BBFV,ISPC,D,H,TKILL,
     1               ICR(I),BARK,ITRUNC(I),
     1               VMAX, BFMAX, CULL(I),DECAYCD(I),WDLDSTEM(I),
     1               BIOMAS,LIVEDEAD,CTKFLG,BTKFLG,IT)
      ELSEIF (METHC(ISPC).EQ.8) THEN
        CALL OCFVOL (VN,VM,ISPC,D,H,TKILL,BARK,ITRUNC(I),VMAX,LCONE,
     1               CTKFLG,IT)
      ELSE
        CALL CFVOL (ISPC,D,H,D2H,VN,VM,VMAX,TKILL,LCONE,BARK,ITRUNC(I),
     1              CTKFLG)
      ENDIF
      IF(VEQNNC(ISPC)(1:3) .NE. 'NVB'.AND. .NOT. LFIANVB .AND.
     1  (CTKFLG .AND. TKILL .AND. VMAX .GT. 0.))
     1 CALL CFTOPK (ISPC,D,H,TCF,MCF,SCF,VMAX,LCONE,BARK,ITRUNC(I))
C----------
C  LOAD WK1 WITH MERCH CUBIC VOLUME PER TREE.
C  WK1 array has been replaced with MCFV
C----------
!      WK1(I)=VM
       MCFV(I)=MCF
       IF(VARACD.EQ.'CS' .OR. VARACD.EQ.'LS' .OR. VARACD.EQ.'NE' .OR.
     > VARACD.EQ.'SN' .OR. LFIANVB) SCFV(I)=SCF

       IF(LFIANVB) THEN
         READ(FIAJSP(ISPC),'(I4)')IFIASP
         DO K=1,2677
            IF(WDBKWT(K,1).EQ.IFIASP) THEN
              CARBFACTOR = WDBKWT(K,12)
              EXIT
            ENDIF
         ENDDO
         ABVGRD_BIO(I) = BIOMAS(1)
         FOLI_BIO(I) = BIOMAS(13)
         ABVGRD_CARB(I) = BIOMAS(15)
         FOLI_CARB(I) = BIOMAS(13)*0.5
C------- MAKE SURE TREE(I) GETS ASSIGNED A VALUE
         MERCH_BIO(I) = 0
         MERCH_CARB(I) = 0
         CUBSAW_BIO(I) = 0
         CUBSAW_CARB(I) = 0

         IF(LIVEDEAD .EQ.'L' .AND. DECAYCD(I) .GT. 0) DECAYCD(I) = 0
         IF(DECAYCD(I).GT.0) THEN
           SELECT CASE(DECAYCD(I))
             CASE(1)
               CARBFACTOR = 0.501
               IF(IFIASP.GE.300) CARBFACTOR = 0.47
             CASE(2)
               CARBFACTOR = 0.504
               IF(IFIASP.GE.300) CARBFACTOR = 0.473
             CASE(3)
               CARBFACTOR = 0.506
               IF(IFIASP.GE.300) CARBFACTOR = 0.481
             CASE(4)
               CARBFACTOR = 0.52
               IF(IFIASP.GE.300) CARBFACTOR = 0.48
             CASE(5)
               CARBFACTOR = 0.527
               IF(IFIASP.GE.300) CARBFACTOR = 0.472
           END SELECT
         ENDIF
         CARB_FRAC(I) = CARBFACTOR

         SELECT CASE (IFIASP)
           CASE (62,  63,  65,  66, 69, 106, 133, 134, 
     &          143, 321, 322, 475, 803, 810, 814, 843)
              MCFV(I)        = 0
              SCFV(I)        = 0
              MERCH_BIO(I)   = 0
              MERCH_CARB(I)  = 0
              CUBSAW_BIO(I)  = 0
              CUBSAW_CARB(I) = 0
           CASE DEFAULT
            IF(D .GE. DBHMIN(ISPC)) THEN
              MERCH_BIO(I) = BIOMAS(6) + BIOMAS(8)
              MERCH_CARB(I) = MERCH_BIO(I) * CARBFACTOR
            ENDIF
            IF(D .GE. SCFMIND(ISPC)) THEN
              CUBSAW_BIO(I) = BIOMAS(6)
              CUBSAW_CARB(I) = CUBSAW_BIO(I)*CARBFACTOR
            ENDIF
          END SELECT
        ENDIF

C----------
C  SUMMARIZE VOLUME BY SPECIES AND TREE CLASS.  IF LSTART IS
C  FALSE, LOAD WK5 AND SUMMARIZE ACCRETION BY SPECIES AND TREE
C  CLASS. DO NOT DO THIS FOR CYCLE 0 DEAD TREES.
C----------
      IF(IPASS .EQ. 2) GO TO 15
      IM=IMC(I)
      IF(.NOT.LSTART) THEN
        IF(CFV(I).GT.TCF)THEN
          WK5(I)=0.
        ELSE
          WK5(I)=(TCF-CFV(I))*P/FINT
        ENDIF
        SPCAC(ISPC,IM)=SPCAC(ISPC,IM)+WK5(I)
      ENDIF
      SPCCC(ISPC,IM)=SPCCC(ISPC,IM)+TCF*P
C----------
C  LOAD CFV WITH TOTAL CUBIC VOLUME PER TREE.
C----------
   15 CONTINUE
      CFV(I)= TCF
      ICDF= 0

      IF (VARACD .EQ. 'CS' .OR. VARACD .EQ. 'LS'
     >     .OR. VARACD .EQ. 'NE' .OR. VARACD .EQ. 'SN') THEN
        TEMVOL= MCFV(I)-SCFV(I)
      ELSE
        TEMVOL= MCFV(I)
      END IF

      ICDF= DEFECT(I)/1000000
      IF(MCFV(I).GT.0.0 .AND. LCVOLS) THEN
C----------
C       COMPUTE DEFECT CORRECTION FACTOR FOR CUBIC FOOT VOLUME.
C       TAKE LARGEST OF 1) INPUT CF DEFECT PERCENT, 2) COMPUTED LINEAR
C       INTERPOLATION VALUE, OR 3) COMPUTED LOG-LINEAR MODEL VALUE.
C----------
        DLIEQN=NINT(ALGSLP(D,DBHCLS,CFDEFT(1,ISPC),9) * 100.)
        IF(DLIEQN.GT.ICDF) ICDF=DLIEQN
        IF(CFLA0(ISPC).EQ.0.0 .AND. CFLA1(ISPC).EQ.1.0) THEN
          VOLCOR=TEMVOL
        ELSE
          VOLCOR=EXP(CFLA0(ISPC)+CFLA1(ISPC)*ALOG(TEMVOL))
        ENDIF
        IF(TEMVOL .EQ. 0) GO TO 25
        DLLMOD=NINT(((TEMVOL-VOLCOR)/TEMVOL) * 100.)
        IF(DLLMOD.GT.ICDF) ICDF=DLLMOD
   25   CONTINUE
        IF(ICDF.GT.99) ICDF=99
        IF(ICDF.LT. 0) ICDF= 0
      ENDIF
C----------
C        CORRECT MERCHANTABLE CUBIC VOLUME FOR FORM AND DEFECT.
C        CONSIDER 99% DEFECT AS 100% DEFECT.
C----------
      IF (VARACD .EQ. 'CS' .OR. VARACD .EQ. 'LS'
     >     .OR. VARACD .EQ. 'NE' .OR. VARACD .EQ. 'SN') THEN
        PULPV= 0
        IF (ICDF.LT.99) THEN
          PULPV= MCFV(I)-SCFV(I)
          PULPV= PULPV*(1.-FLOAT(ICDF)/100)
        END IF
      ELSE
        IF(ICDF.LT.99) THEN
          MCFV(I)= MCFV(I)*(1.-FLOAT(ICDF)/100.)
        ELSE
          MCFV(I)=0.
        END IF
      END IF
      CONTINUE
C----------
C  CALL ECON EXTENSION TO PROCESS VOLUME INFORMATION
C----------
      IF (VARACD .EQ. 'CS' .OR. VARACD .EQ. 'LS' 
     >     .OR. VARACD .EQ. 'NE' .OR. VARACD .EQ. 'SN') THEN
        IF (SCFV(I) .GT. 0.0) CALL ECVOL(IT,LOGDIA,LOGVOL,.TRUE.)
      ELSE
        IF (MCFV(I) .GT. 0.0) CALL ECVOL(IT,LOGDIA,LOGVOL,.TRUE.)
      END IF
*      CALL ECVOL(ISPC,IT,LOGDIA,LOGVOL,.TRUE.)
C**************************************************
C           BOARD VOLUME SECTION                  *
C**************************************************
C----------
C  COMPUTE SCRIBNER BOARD FOOT VOLUME TO A VARIABLE TOP FOR TREES
C  OF MINIMUM MERCHANTABLE DBH AND LARGER.
C----------
      BFV(I)=0.0
      IBDF= DEFECT(I)/10000 - (DEFECT(I)/1000000)*100

      IF((D.LT.BFMIND(ISPC).OR.D.LE.BFTOPD(ISPC)).AND.SCFV(I).GT.0) THEN 
        GO TO 100
      ELSE IF(D.LT.BFMIND(ISPC).OR.D.LE.BFTOPD(ISPC)) THEN
        GO TO 150
      END IF
C----------
C   CALCULATE SCRIBNER BOARD FOOT VOLUME.
C----------
      IF(DEBUG)WRITE(JOSTND,*)' BOARD SECTION, I,ISPC,D,H,METHB= ',
     &I,ISPC,D,H,METHB(ISPC)
C
      IF(((VARACD.EQ.'NE' .OR. VARACD.EQ.'CS' .OR. VARACD.EQ.'LS') 
     +    .AND. METHB(ISPC).EQ.5 )
     +    .OR. METHB(ISPC).EQ.6 .OR. METHB(ISPC).EQ.9) THEN
        IF(METHC(ISPC).EQ.6 .OR. METHC(ISPC).EQ.10) THEN
          GO TO 100
        ELSE
          CALL NATCRS (TCF,MCF,SCF,BBFV,ISPC,D,H,TKILL,
     1                 ICR(I),BARK,ITRUNC(I),
     1                 VMAX,BFMAX,CULL(I),DECAYCD(I),WDLDSTEM(I),
     1                 BIOMAS,LIVEDEAD,CTKFLG,BTKFLG,IT)
        ENDIF
      ELSEIF (METHB(ISPC).EQ.8) THEN
        IT=I
        CALL OBFVOL (BBFV,ISPC,D,H,TKILL,BARK,ITRUNC(I),BFMAX,LCONE,
     1               BTKFLG,IT)
      ELSE
        CALL BFVOL (ISPC,D,H,D2H,BBFV,TKILL,LCONE,BARK,BFMAX,ITRUNC(I),
     1              BTKFLG)
      ENDIF

C----------
C  CORRECT FOR TOPKILL IF NEEDED.
C  LOAD BFV WITH SCRIBNER BOARD FOOT VOLUME CORRECTED FOR TOPKILL.
C----------
  100 CONTINUE
      IF(BTKFLG .AND. TKILL .AND. BFMAX .GT. 0.)
     1 CALL BFTOPK (ISPC,D,H,BBFV,LCONE,BARK,BFMAX,ITRUNC(I))
      BFV(I)=BBFV
      IF(BFV(I).GT.0.0 .AND. LBVOLS) THEN
C----------
C     COMPUTE DEFECT CORRECTION FACTOR FOR BOARD FOOT VOLUME.
C     TAKE LARGEST OF 1) INPUT BF DEFECT PERCENT, 2) COMPUTED LINEAR
C     INTERPOLATION BF VALUE, OR 3) COMPUTED LOG-LINEAR MODEL BF VALUE.
C----------
        DLIEQN=NINT(ALGSLP(D,DBHCLS,BFDEFT(1,ISPC),9) * 100.)
        IF(DLIEQN.GT.IBDF)IBDF=DLIEQN
        IF(BFLA0(ISPC).EQ.0.0 .AND. BFLA1(ISPC).EQ.1.0) THEN
          VOLCOR=BFV(I)
        ELSE
          VOLCOR=EXP(BFLA0(ISPC)+BFLA1(ISPC)*ALOG(BFV(I)))
        ENDIF
        DLLMOD=NINT(((BFV(I)-VOLCOR)/BFV(I)) * 100.)
        IF(DLLMOD.GT.IBDF)IBDF=DLLMOD
        IF(IBDF.GT.99)IBDF=99
        IF(IBDF.LT. 0)IBDF= 0
      ENDIF
C----------
C        CORRECT MERCHANTABLE BOARD FOOT VOLUME FOR FORM AND DEFECT.
C        CONSIDER 99% DEFECT AS 100% DEFECT.
C----------
      IF(IBDF.LT.99) THEN
        BFV(I)=BFV(I)*(1.-FLOAT(IBDF)/100.)
        IF(.NOT.LFIANVB) SCFV(I)=SCFV(I)*(1.-FLOAT(IBDF)/100.)
      ELSE
        BFV(I)=0.
        IF(.NOT.LFIANVB) SCFV(I)=0.
      ENDIF

C----------
C  CALL ECON EXTENSION TO PROCESS VOLUME INFORMATION
C----------
      IF (BFV(I) .GT. 0.0) CALL ECVOL(IT,LOGDIA,LOGVOL,.FALSE.)
*      CALL ECVOL(ISPC,IT,LOGDIA,LOGVOL,.FALSE.)
C----------
C  RESET NEGATIVE VOLUMES TO ZERO AND COMPILE TOTAL.
C----------
      IF(BFV(I).LT.0.0) BFV(I)=0.0
  150 CONTINUE
C----------
C     RECOMBINE DEFECT CORRECTED PULPWOOD AND CUBIC SAW LOG
C     FOR EASTERN VARIANTS
C----------
      IF (VARACD.EQ.'NE' .OR. VARACD.EQ.'CS'
     +    .OR. VARACD.EQ.'LS' .OR. VARACD.EQ.'SN') THEN
        MCFV(I)= PULPV+SCFV(I)
      END IF

      IF(IPASS .EQ. 1) THEN
        SPCBV(ISPC,IM)= SPCBV(ISPC,IM)+BFV(I)*P
        SPCMC(ISPC,IM)= SPCMC(ISPC,IM)+MCFV(I)*P
        SPCSC(ISPC,IM)= SPCSC(ISPC,IM)+SCFV(I)*P
      END IF
C----------
C  SET VALUES IN CODED DEFECT NUMBER TO INDICATE THE ACTUAL DEFECT
C  PERCENTAGES APPLIED THIS CYCLE.
C  DEFECT CODED AS 11223344, WHERE 11=INPUT MC DEFECT %, 22=INPUT BF
C  DEFECT PERCENT, 33=MC DEFECT % USED THIS CYCLE, 44=BF DEFECT % USED
C  THIS CYCLE.
C----------
      DEFECT(I)=((DEFECT(I)/10000)*10000) + ICDF*100 + IBDF
      IF(DEBUG)WRITE(JOSTND,*)' IN VOLS, I= ',I,' DEFECT= ',DEFECT(I)
C----------
C  PRINT DEBUG OUTPUT IF DESIRED
C----------
      IF(.NOT.DEBUG) GO TO 200
      WRITE(JOSTND,9000)I,TCF,MCFV(I),WK5(I),CFV(I),
     &     SPCAC(ISPC,1),SPCAC(ISPC,2),SPCAC(ISPC,3),SPCCC(ISPC,1),
     &     SPCCC(ISPC,2),SPCCC(ISPC,3),ISPC,D,H
 9000 FORMAT(' IN VOLS, I=',I4,' TCF=',E15.6,' MCFV=',E15.6,/,
     &  '  WK5=',E15.6,' CFV=',E15.6,
     &  ' SPCAC=',3(E15.6,',')/'  SPCCC=',3(E15.6,','),' ISPC=',I2,
     &  ' DBH=',E11.2,' NORMAL HT=',E11.2)
C----------
C  END OF TREE LOOP.
C----------
  200 CONTINUE
  205 CONTINUE
      IF(IPASS .EQ. 2) GO TO 250
C----------
C  DETERMINE SPECIES-TREE CLASS COMPOSITION FOR ALL VOLUME STANDARDS.
C----------
      CALL COMP(OSPCV,IOSPCV,SPCCC)
      CALL COMP(OSPBV,IOSPBV,SPCBV)
      CALL COMP(OSPMC,IOSPMC,SPCMC)
      CALL COMP(OSPSC,IOSPSC,SPCSC)
C----------
C  DETERMINE SPECIES-TREE CLASS COMPOSITION AND PERCENTILE POINTS IN
C  THE DISTRIBUTION OF DIAMETERS FOR ANNUAL TOTAL CUBIC FOOT ACCRETION.
C  BYPASS IF LSTART IS TRUE.
C----------
      IF(LSTART) GO TO 210
      CALL COMP(OSPAC,IOSPAC,SPCAC)
      CALL PCTILE(ITRN,IND,WK5,WK3,OACC(7))
      CALL DIST(ITRN,OACC,WK3)
  210 CONTINUE
C----------
C  CALCULATE VOLUMES FOR CYCLE 0 DEAD TREES.
C----------
      IF(IREC2 .GT. MAXTRE) GO TO 250
      IPASS=2
      ILOW=IREC2
      IHI=MAXTRE
      GO TO 10
C----------
C  END OF VOLS.
C----------
  250 CONTINUE
      DO I=1,10
        IF(DEBUG)WRITE(JOSTND,*)' LEAVING VOLS, I,CFV,BFV= ',
     *    I,CFV(I),BFV(I)
      ENDDO
      RETURN
      END
